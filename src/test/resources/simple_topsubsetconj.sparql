PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
SELECT ?atomb ?atomc
WHERE { owl:Thing     rdfs:subClassOf                          ?object
#                 ; rdf:type                                 owl:Class
                 .
        # FIXME: would like to write ?object owl:intersectionOf/owl:Class/rdf:first ?atomb here, but it doesn't work.
        ?object     owl:intersectionOf                      ?conj
                 ;  rdf:type                                owl:Class 
                 .
        ?conj       rdf:first                               ?atomb
                 ;  rdf:rest/rdf:first                      ?atomc
                    # Ensure this is a binary intersectionOf
                 ;  rdf:rest/rdf:rest                       rdf:nil
                 .
        ?atomb     rdf:type                                 owl:Class 
                 .
        ?atomc     rdf:type                                 owl:Class 
                 .
        # Ensure each atom is atomic 
        FILTER NOT EXISTS{?atomb owl:unionOf        ?o1 } 
        FILTER NOT EXISTS{?atomb owl:intersectionOf ?o2 } 
        FILTER NOT EXISTS{?atomb owl:complementOf   ?o3 } 
        FILTER NOT EXISTS{?atomc owl:unionOf        ?o4 } 
        FILTER NOT EXISTS{?atomc owl:intersectionOf ?o5 } 
        FILTER NOT EXISTS{?atomc owl:complementOf   ?o6 } 
       } 
GROUP BY ?atomb ?atomc